plugins {
    id 'java-library'
    id 'maven-publish'
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/Foundation-Devices/jni-utils-rs")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
                password = project.findProperty("gpr.token") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = 'Extra Utilities for JNI in Rust'
                description = 'This is the Java support library for jni-utils.'
                url = 'https://github.com/gedgygedgy/jni-utils-rs'
                licenses {
                    license {
                        name = '3-Clause BSD License'
                        url = 'https://opensource.org/licenses/BSD-3-Clause'
                    }
                }
                developers {
                    developer {
                        id = 'gedgygedgy'
                        name = 'Gedgy Gedgy'
                        email = 'gedgygedgy@protonmail.com'
                        organization = 'Gedgy Gedgy'
                        organizationUrl = 'https://github.com/gedgygedgy'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/Foundation-Devices/jni-utils-rs.git'
                    developerConnection = 'scm:git:ssh://github.com:Foundation-Devices/jni-utils-rs.git'
                    url = 'https://github.com/Foundation-Devices/jni-utils-rs'
                }
            }
        }
    }
}

group = 'io.github.gedgygedgy.rust'
version = '0.1.1-SNAPSHOT'

sourceCompatibility = 1.8
